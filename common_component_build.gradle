if (isDebugModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'com.jakewharton.butterknife'

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode build_versions.version_code
        versionName build_versions.version_name
        //ARouter的配置
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
                includeCompileClasspath true
            }
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            buildConfigField "boolean", "USE_CANARY", "true"
            buildConfigField "boolean", "IS_DEBUG_MODULE", "${isDebugModule}"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            buildConfigField "boolean", "USE_CANARY", "false"
            buildConfigField "boolean", "IS_DEBUG_MODULE", "${isDebugModule}"
            minifyEnabled true
            //去除无用资源--debug模式才可以使用改配置，否则提示：Error:Resource shrinker cannot be used for libraries.
            if (isDebugModule.toBoolean()) {
                shrinkResources true
            }
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        disable "ResourceType"
        abortOnError false
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            //根据模式添加配置文件
            if (isDebugModule.toBoolean()) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }
}

dependencies {
    if (isDebugModule.toBoolean()) {
        //debug模式，编译时执行，不会打入APK包中
        annotationProcessor deps.butterknife_compiler
        annotationProcessor deps.dagger.android
        annotationProcessor deps.dagger.android_support
        annotationProcessor deps.dagger.android_support_compiler
        annotationProcessor deps.arouter_compiler
        debugImplementation deps.leakcanary
    } else {
        compileOnly deps.butterknife_compiler
        compileOnly deps.dagger.android
        compileOnly deps.dagger.android_support
        compileOnly deps.arouter_compiler
        debugImplementation deps.leakcanary
    }
}
